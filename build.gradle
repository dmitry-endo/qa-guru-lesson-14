plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

group = 'ru.infosec'
version = '1.0-SNAPSHOT'

def allureVersion = "2.21.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set(allureVersion) //версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            'org.junit.jupiter:junit-jupiter:5.10.1',
            'com.codeborne:selenide:7.9.1',
            'org.slf4j:slf4j-simple:2.0.7',
            'org.assertj:assertj-core:3.27.3',
            'io.qameta.allure:allure-selenide:2.29.1'
    )
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task tj_simple_tests(type: Test) {
    useJUnitPlatform {
        includeTags("tj_simple_tests")
    }
}
